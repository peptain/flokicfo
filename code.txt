//SPDX-License-Identifier: MIT

pragma solidity ^0.8.14;

import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@uniswap/v2-periphery/contracts/interfaces/IUniswapV2Router02.sol";
import "@uniswap/v2-core/contracts/interfaces/IUniswapV2Factory.sol";
import "./DividendDistributor.sol";

contract FlokiCFO is IERC20, Ownable {
    address DEAD = 0x000000000000000000000000000000000000dEaD;
    address ZERO = 0x0000000000000000000000000000000000000000;

    address public REDFLOKI = 0x3c0fE6c4aCd3a21615a51372D2A430Eb68cCde43;
    address public FLOKICEO = 0x45289007706E7Ee7B42b1fA506661d97740Edfb4;

    string constant _name = "Floki CFO";
    string constant _symbol = "FlokiCFO";
    uint8 constant _decimals = 9;

    uint256 _totalSupply = 1000000000 * (10 ** _decimals);

    mapping(address => uint256) _balances;
    mapping(address => mapping(address => uint256)) _allowances;

    mapping(address => bool) public isFeeExempt;
    mapping(address => bool) public isDividendExempt;
    mapping(address => bool) public isAuthorized;

    address public marketingWallet;

    // Fees

    uint256 public buyRedRewardFee = 0;
    uint256 public buyCEORewardFee = 0;
    uint256 public buyMarketingFee = 0;
    uint256 public buyTotalFee = 0;

    uint256 public sellRedRewardFee = 3;
    uint256 public sellCEORewardFee = 3;
    uint256 public sellMarketingFee = 4;
    uint256 public sellTotalFee = 10;

    IUniswapV2Router02 public router;
    address public pair;

    uint256 public listingTime;

    DividendDistributor public redFlockiDividendTracker;
    DividendDistributor public flokiCEODividendTracker;

    uint256 distributorGas = 500000;

    bool public getTransferFees = false;

    uint256 public swapThreshold = (_totalSupply * 1) / 10000; // 0.001% of supply
    bool public contractSwapEnabled = true;
    bool public isTradeEnabled = false;
    bool inContractSwap;
    modifier swapping() {
        inContractSwap = true;
        _;
        inContractSwap = false;
    }

    event SetIsDividendExempt(address holder, bool status);
    event SetIsFeeExempt(address holder, bool status);
    event AddAuthorizedWallet(address holder, bool status);
    event SetDoContractSwap(bool status);
    event DoContractSwap(uint256 amount, uint256 time);
    event ChangeDistributionCriteria(
        uint256 minPeriod,
        uint256 minDistribution
    );

    event AutoLiquify(uint256 amountBNB, uint256 amountBOG);

    constructor() {
        router = IUniswapV2Router02(0x10ED43C718714eb63d5aA57B78B54704E256024E);
        pair = IUniswapV2Factory(router.factory()).createPair(
            router.WETH(),
            address(this)
        );
        _allowances[address(this)][address(router)] = type(uint256).max;

        redFlockiDividendTracker = new DividendDistributor(REDFLOKI);
        flokiCEODividendTracker = new DividendDistributor(FLOKICEO);

        marketingWallet = 0x2433E36dC7D27606d9e863B5194380e2BE42a720;

        address deployer = 0xa6a8a4c9AAf02bF15A4386821FDA61A99A58437a;

        isFeeExempt[deployer] = true;
        isFeeExempt[address(this)] = true;
        isFeeExempt[marketingWallet] = true;

        isDividendExempt[deployer] = true;
        isDividendExempt[pair] = true;
        isDividendExempt[address(this)] = true;
        isDividendExempt[ZERO] = true;
        isDividendExempt[DEAD] = true;

        isDividendExempt[marketingWallet] = true;

        isAuthorized[deployer] = true;
        isAuthorized[address(this)] = true;
        isAuthorized[ZERO] = true;
        isAuthorized[DEAD] = true;

        isAuthorized[marketingWallet] = true;

        _balances[deployer] = _totalSupply;
        emit Transfer(address(0), deployer, _totalSupply);
    }

    receive() external payable {}

    function totalSupply() external view override returns (uint256) {
        return _totalSupply;
    }

    function name() public pure returns (string memory) {
        return _name;
    }

    function symbol() public pure returns (string memory) {
        return _symbol;
    }

    function decimals() public pure returns (uint8) {
        return _decimals;
    }

    function balanceOf(address account) public view override returns (uint256) {
        return _balances[account];
    }

    function allowance(
        address holder,
        address spender
    ) external view override returns (uint256) {
        return _allowances[holder][spender];
    }

    function approve(
        address spender,
        uint256 amount
    ) public override returns (bool) {
        _allowances[msg.sender][spender] = amount;
        emit Approval(msg.sender, spender, amount);
        return true;
    }

    function _approve(
        address owner,
        address spender,
        uint256 amount
    ) internal virtual {
        require(owner != address(0), "ERC20: approve from the zero address");
        require(spender != address(0), "ERC20: approve to the zero address");

        _allowances[owner][spender] = amount;
        emit Approval(owner, spender, amount);
    }

    function approveMax(address spender) external returns (bool) {
        return approve(spender, type(uint256).max);
    }

    function transfer(
        address recipient,
        uint256 amount
    ) external override returns (bool) {
        return _transferFrom(msg.sender, recipient, amount);
    }

    function transferFrom(
        address sender,
        address recipient,
        uint256 amount
    ) external override returns (bool) {
        if (_allowances[sender][msg.sender] != type(uint256).max) {
            require(
                _allowances[sender][msg.sender] >= amount,
                "Insufficient Allowance"
            );
            _allowances[sender][msg.sender] =
                _allowances[sender][msg.sender] -
                amount;
        }

        return _transferFrom(sender, recipient, amount);
    }

    function _transferFrom(
        address sender,
        address recipient,
        uint256 amount
    ) internal returns (bool) {
        if (!isTradeEnabled) require(isAuthorized[sender], "Trading disabled");
        if (inContractSwap) {
            return _basicTransfer(sender, recipient, amount);
        }

        if (shouldDoContractSwap()) {
            doContractSwap();
        }

        require(_balances[sender] >= amount, "Insufficient Balance");
        _balances[sender] = _balances[sender] - amount;

        uint256 amountReceived = shouldTakeFee(sender, recipient)
            ? takeFee(sender, recipient, amount)
            : amount;
        _balances[recipient] = _balances[recipient] + amountReceived;

        if (!isDividendExempt[sender]) {
            try
                redFlockiDividendTracker.setShare(sender, _balances[sender])
            {} catch {}
        }

        if (!isDividendExempt[recipient]) {
            try
                redFlockiDividendTracker.setShare(
                    recipient,
                    _balances[recipient]
                )
            {} catch {}
        }

        try redFlockiDividendTracker.process(distributorGas) {} catch {}

        // flockiCEO
        if (!isDividendExempt[sender]) {
            try
                flokiCEODividendTracker.setShare(sender, _balances[sender])
            {} catch {}
        }

        if (!isDividendExempt[recipient]) {
            try
                flokiCEODividendTracker.setShare(
                    recipient,
                    _balances[recipient]
                )
            {} catch {}
        }

        try flokiCEODividendTracker.process(distributorGas) {} catch {}

        emit Transfer(sender, recipient, amountReceived);
        return true;
    }

    function takeFee(
        address sender,
        address recipient,
        uint256 amount
    ) internal returns (uint256) {
        uint256 feeToken;

        if (recipient == pair) feeToken = (amount * sellTotalFee) / 100;
        else feeToken = (amount * buyTotalFee) / 100;

        _balances[address(this)] = _balances[address(this)] + feeToken;
        emit Transfer(sender, address(this), feeToken);

        return (amount - feeToken);
    }

    function _basicTransfer(
        address sender,
        address recipient,
        uint256 amount
    ) internal returns (bool) {
        require(_balances[sender] >= amount, "Insufficient Balance");
        _balances[sender] = _balances[sender] - amount;

        _balances[recipient] = _balances[recipient] + amount;
        emit Transfer(sender, recipient, amount);
        return true;
    }

    function shouldTakeFee(
        address sender,
        address to
    ) internal view returns (bool) {
        if (!getTransferFees) {
            if (sender != pair && to != pair) return false;
        }
        if (isFeeExempt[sender] || isFeeExempt[to]) {
            return false;
        } else {
            return true;
        }
    }

    function shouldDoContractSwap() internal view returns (bool) {
        return (msg.sender != pair &&
            !inContractSwap &&
            contractSwapEnabled &&
            sellTotalFee != 0 &&
            _balances[address(this)] >= swapThreshold);
    }

    function isRewardExcluded(address _wallet) public view returns (bool) {
        return isDividendExempt[_wallet];
    }

    function isFeeExcluded(address _wallet) public view returns (bool) {
        return isFeeExempt[_wallet];
    }

    function doContractSwap() internal swapping {
        uint256 contractTokenBalance = _balances[address(this)];

        uint256 tokensToMarketing = (contractTokenBalance * sellMarketingFee) /
            sellTotalFee;
        uint256 tokensToRedRewards = (contractTokenBalance * sellRedRewardFee) /
            sellTotalFee;

        uint256 tokensToCeoRewards = contractTokenBalance -
            tokensToMarketing -
            tokensToRedRewards;

        if (tokensToRedRewards > 0) {
            swapTokensForTokens(tokensToRedRewards, REDFLOKI);

            uint256 swappedRewardTokens = IERC20(REDFLOKI).balanceOf(
                address(this)
            );
            IERC20(REDFLOKI).transfer(
                address(redFlockiDividendTracker),
                swappedRewardTokens
            );
            try
                redFlockiDividendTracker.deposit(swappedRewardTokens)
            {} catch {}
        }
        if (tokensToCeoRewards > 0) {
            swapTokensForTokens(tokensToCeoRewards, FLOKICEO);

            uint256 swappedRewardTokens = IERC20(FLOKICEO).balanceOf(
                address(this)
            );
            IERC20(FLOKICEO).transfer(
                address(flokiCEODividendTracker),
                swappedRewardTokens
            );
            try flokiCEODividendTracker.deposit(swappedRewardTokens) {} catch {}
        }
        if (tokensToMarketing > 0) {
            swapTokensForEth(tokensToMarketing);

            uint256 swappedTokens = address(this).balance;

            if (swappedTokens > 0)
                payable(marketingWallet).transfer(swappedTokens);
        }
    }

    // All tax wallets receive BUSD instead of BNB
    function swapTokensForTokens(
        uint256 tokenAmount,
        address tokenToSwap
    ) private {
        address[] memory path = new address[](3);
        path[0] = address(this);
        path[1] = router.WETH();
        path[2] = tokenToSwap;
        _approve(address(this), address(router), tokenAmount);
        router.swapExactTokensForTokensSupportingFeeOnTransferTokens(
            tokenAmount,
            0, // accept any amount of tokens
            path,
            address(this),
            block.timestamp
        );
    }

    function swapTokensForEth(uint256 tokenAmount) private {
        // generate the uniswap pair path of token -> weth
        address[] memory path = new address[](2);
        path[0] = address(this);
        path[1] = router.WETH();
        _approve(address(this), address(router), tokenAmount);
        // make the swap
        router.swapExactTokensForETHSupportingFeeOnTransferTokens(
            tokenAmount,
            0, // accept any amount of ETH
            path,
            address(this),
            block.timestamp
        );
    }

    function setIsDividendExempt(
        address holder,
        bool exempt
    ) external onlyOwner {
        require(
            holder != address(this) && holder != pair,
            "can not add pair and token address as share holder"
        );
        isDividendExempt[holder] = exempt;
        if (exempt) {
            redFlockiDividendTracker.setShare(holder, 0);
            flokiCEODividendTracker.setShare(holder, 0);
        } else {
            redFlockiDividendTracker.setShare(holder, _balances[holder]);
            flokiCEODividendTracker.setShare(holder, _balances[holder]);
        }

        emit SetIsDividendExempt(holder, exempt);
    }

    function setIsFeeExempt(address holder, bool exempt) external onlyOwner {
        isFeeExempt[holder] = exempt;

        emit SetIsFeeExempt(holder, exempt);
    }

    function setDoContractSwap(bool _enabled) external onlyOwner {
        contractSwapEnabled = _enabled;

        emit SetDoContractSwap(_enabled);
    }

    function setDistributionCriteria(
        uint256 _minPeriod,
        uint256 _minDistribution
    ) external onlyOwner {
        redFlockiDividendTracker.setDistributionCriteria(
            _minPeriod,
            _minDistribution
        );
        flokiCEODividendTracker.setDistributionCriteria(
            _minPeriod,
            _minDistribution
        );

        emit ChangeDistributionCriteria(_minPeriod, _minDistribution);
    }

    function setDistributorSettings(uint256 gas) external onlyOwner {
        require(gas < 750000);
        distributorGas = gas;
    }

    function changeMarketingWallet(address _wallet) external onlyOwner {
        marketingWallet = _wallet;
    }

    function changeBuyFees(
        uint256 _buyRedRewardFee,
        uint256 _buyCEORewardFee,
        uint256 _marketingFee
    ) external onlyOwner {
        require(
            (listingTime + 7 days) <= block.timestamp,
            "can not change fees for 7 days"
        );
        buyRedRewardFee = _buyRedRewardFee;
        buyMarketingFee = _marketingFee;
        buyCEORewardFee = _buyCEORewardFee;

        buyTotalFee = _buyRedRewardFee + _marketingFee + _buyCEORewardFee;

        require(buyTotalFee <= 10, "Total fees can not greater than 10%");
    }

    function changeSellFees(
        uint256 _sellRedRewardFee,
        uint256 _sellCEORewardFee,
        uint256 _marketingFee
    ) external onlyOwner {
        require(
            (listingTime + 7 days) <= block.timestamp,
            "can not change fees for 7 days"
        );
        sellRedRewardFee = _sellRedRewardFee;
        sellCEORewardFee = _sellCEORewardFee;
        sellMarketingFee = _marketingFee;

        sellTotalFee = _sellRedRewardFee + _marketingFee + _sellCEORewardFee;

        require(sellTotalFee <= 10, "Total fees can not greater than 10%");
    }

    function enableTrading() external onlyOwner {
        require(!isTradeEnabled, "Trading already enabled");
        isTradeEnabled = true;
        listingTime = block.timestamp;
    }

    function setAuthorizedWallets(
        address _wallet,
        bool _status
    ) external onlyOwner {
        isAuthorized[_wallet] = _status;
    }

    function rescueBNB() external onlyOwner {
        uint256 balance = address(this).balance;
        require(balance > 0, "No enough BNB to transfer");

        payable(msg.sender).transfer(balance);
    }

    function changeGetFeesOnTransfer(bool _status) external onlyOwner {
        getTransferFees = _status;
    }

    function changePair(address _pair) external onlyOwner {
        pair = _pair;
    }
}